import tech.tablesaw.columns.*

component("bootstrap_select"){
    options.small = false
    options.class = "mt-3"
    div(class: "form-group ${options.class}") {
        if (options.label) {
            label(class: "form-label", options.label)
        }
        //TODO: disable in static mode?
        select(class: "form-select ${options.small ?'form-select-sm' : '' }", id: "combobox", name: options.param, "data-magic-param" : true ) {

            if (options.values instanceof List) {
                for (value in options.values) {
                    def properties = [
                        value: value instanceof Column ? value.name() : value
                    ]
                    if (properties.value == params[options.param]) {
                        properties.selected = "selected"
                    }
                    option(properties, properties.value)
                }
            }
            if (options.values instanceof Map) {
                for (key in options.values.keySet()) {
                    def value = options.values.get(key)
                    def properties = [
                        value: key
                    ]
                    if (properties.value == params[options.param]) {
                        properties.selected = "selected"
                    }
                    option(properties, value)
                }
            }
        }
    }
}

component("bootstrap_number"){
     div(class: "form-group") {
        if (options.label) {
            label(class: "form-label", options.label)
        }
        //TODO: disable in static mode?
        input(type: "number", class: "form-control", id: "combobox", name: options.param, "data-magic-param" : true, value: params[options.param] ) {
        }
    }
}

component("bootstrap_text"){
     div(class: "form-group") {
        if (options.label) {
            label(class: "form-label", options.label)
        }
        //TODO: disable in static mode?
        input(class: "form-control", id: "combobox", name: options.param, "data-magic-param" : true, value: params[options.param] ) {
        }
    }
}

component("bootstrap_checkbox"){
    div(class: "form-check") {
        def properties = [
            type: "checkbox",
            class: "form-check-input",
            id: "checkboxInput",
            name: options.param,
            "data-magic-param" : true
        ]
        if (params[options.param] == true) {
            properties.checked = "checked"
        }
        input(properties)
        label(class: "form-check-label", for: "checkboxInput", options.label)
    }
}