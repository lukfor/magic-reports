component("bootstrap_card") {
    div(class: "card my-5") {
        div(class: "card-body") {
            if (options.imageUrl) {
                img(src: options.imageUrl, class: "card-img-top", alt: "Card image")
            }
            h5(class: "card-title", options.title)
            div(class: "card-text", render(options.body))
        }
    }
}

component("bootstrap_header") {
    div(class: "px-4 py-5 text-center", style:"background-color: ${options.color}") {
        h1(class: "display-5 fw-bold", options.title)
        div(class: "col-lg-6 mx-auto") {
            p(class: "lead mb-4", options.description)
        }
    }
}

component("bootstrap_table") {
    def headerRow = options.data[0]
    table(class: "table table-bordered table-striped") {
        thead {
            tr {
                for (String header : headerRow) {
                    th(header)
                }
            }
        }
        tbody {
            for (int i = 1; i < options.data.size(); i++) {
                tr {
                    for (String cell : options.data[i]) {
                        td(cell)
                    }
                }
            }
        }
    }
}

def irisData = [
    ["Sepal Length", "Sepal Width", "Petal Length", "Petal Width", "Species"],
    [5.1, 3.5, 1.4, 0.2, "setosa"],
    [4.9, 3.0, 1.4, 0.2, "setosa"],
    // ... more data
]

def age = [2,3,4,5,6,7,8,9]
def salary = [100,200,300,400,500,600,700,800]

report {

    template file("$baseDir/template/index.html")

    header {
        bootstrap_header (
            title: "Iris Dataset",
            description: "The Iris dataset is a well-known dataset in the field of machine learning and statistics. It was introduced by the biologist and statistician Ronald A. Fisher in 1936. The dataset consists of measurements of four features (sepal length, sepal width, petal length, and petal width) for 150 iris flowers, divided into three species: setosa, versicolor, and virginica.",
            color: "#82715f6e"
        )
    }

    content {

        // Using a Card Component to display the Iris dataset
        bootstrap_card (
            title: "Dataset",
            body: {
                p "Dataset Overview"
                bootstrap_table(data: irisData)
            }
        )

        bootstrap_card (
            title: "Plotly22",
            body: {
                p "Simple Scatter plot"
                plotly {
                    trace (
                          x: age,
                          y: salary,
                          mode: "markers",
                          type: "scatter"
                    )
                }
            }
        )

        footer(class: "d-flex flex-wrap justify-content-between align-items-center py-3 my-4 border-top"){
            span(class: "mb-3 mb-md-0 text-muted","This report was created by ${System.getProperty("user.name")} on ${new Date()}")
        }

    }

}
