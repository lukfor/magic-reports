import tech.tablesaw.api.*
import tech.tablesaw.columns.*

def iris = Table.read().csv("examples/iris.csv")

report {

	title "Iris"

    content {

        h2 "Dataset"
        datatable {
            data(iris)

        }

        h2 "Relation between variables"
        p "We will see the relationship between the sepal length and sepal width and also between petal length and petal width."

        h3 "Comparing Sepal Length and Sepal Width"

        plotly {
            scatter (
                  x: iris.column("sepallength").asList(),
                  y: iris.column("sepalwidth").asList(),
                  text: iris.column("class").asList()
            )
        }

        plotly {
            def classes = iris.splitOn(iris.categoricalColumn("class"))
            for (table in classes){
                scatter (
                      x: table.column("sepallength").asList(),
                      y: table.column("sepalwidth").asList(),
                      text: table.column("class").asList(),
                      name: table.column("class").asList()[0]
                )
            }
        }

        plotly {
            box (
                  y: iris.column("sepallength").asList(),
                  x: iris.column("class").asList()
            )
        }

        h2 "Distribution"

        plotly {
            def classes = iris.splitOn(iris.categoricalColumn("class"))
            for (table in classes){
                histogram (
                    x: table.column("sepallength").asList(),
                    name: table.column("class").asList()[0],
                    opacity: 0.5
                )
            }
            layout barmode: "overlay"
        }

        for (className in iris.column("class").asSet()) {
           def filteredData = iris.where(iris.stringColumn("class").isEqualTo(className))

            h3 "${className}"
            plotly {
                histogram (
                      x: filteredData.column("sepallength").asList(),
                      marker: [
                        color: "#aaaaaa"
                     ]
                )
            }
        }

    }

}

